
////////////////////////////////////////////////////
//                                                //
// Disassembled with Nintenlord's Event Assembler //
// Version: 11.0.0.0                              //
// Game: FE8                                      //
// File: FE8_clean.gba                            //
// Offset: $9EC66C                                //
//                                                //
////////////////////////////////////////////////////

EventPointerTable(0x7, PointerList)

setText(0x160, NewChName)

ALIGN 4
NewChName:
String("Ch1: Urban Skirmish")
BYTE 0

ALIGN 4
PointerList:
POIN TurnBasedEvents
POIN CharacterBasedEvents
POIN LocationBasedEvents
POIN MiscBasedEvents
POIN Dunno1 Dunno2 Dunno3 Tutorial
POIN Traps1 Traps2
POIN Units1 Units1  //This unit group determines the number and position of deployment slots when there is a prep screen
POIN $0 $0 $0
POIN $0 $0 $0
POIN BeginningScene EndingScene

TurnBasedEvents:
TurnEventPlayer(0,EnemyReinforcements1,4)
TurnEventPlayer(0,EnemyReinforcements2,5)

#ifdef _EASY_MODE_
TurnEventPlayer(0,BrigandReinforcements1,6)
TurnEventPlayer(0,BrigandReinforcements2,9)
#else
TurnEventPlayer(0,BrigandReinforcements1,5)
TurnEventPlayer(0,BrigandReinforcements2,8)
#endif

// Turn 10 may be too tight for ch1
//TurnEventPlayer(0,EvilReinforcements,10)
TurnEventPlayer(0,EvilReinforcements,11)
TurnEventPlayer(0,EvilReinforcements,12)
TurnEventPlayer(0,EvilReinforcements,13)

TurnEventPlayer(0,StellaEvent,3)
TurnEventPlayer(0,SashaEvent,4)
END_MAIN

EnemyReinforcements1:
EnemyReinforcements(EnemyReinforcementUnits1)

EnemyReinforcements2:
EnemyReinforcements(EnemyReinforcementUnits2)

BrigandReinforcements1:
EnemyReinforcements(BrigandReinforcementUnits1)

BrigandReinforcements2:
EnemyReinforcements(BrigandReinforcementUnits2)

EvilReinforcements:
EnemyReinforcements(EvilReinforcementUnits)

StellaEvent:
CAM1 [0,0]
EnemyReinforcementsNoENDA(StellaReinforcementUnits)
Text(TEXT_Ch1_Stella_Event1)
MOVE 15 0x84 [1,5]
// Scripted battle
StartBattle
MissedAttack(1,0)
CriticalHit(0,25)
EndAttack
ENUN

ENUT 0x84
FIG1 Stella 0x84 Fire
SET_ACTIVE 0
CLEAN
Kill(0x84)

CLEAN
ENUF 0x84
Text(TEXT_Ch1_Stella_Event2)
ENDA


SashaEvent:
//MUSC DW3_Oppression
MUSC AA3_VictorKudo
CAM1 [18,0]
FlashCursor(Sasha,0x20)
FlashCursor(Ch1Boss,0x20)
Text(TEXT_Ch1_Sasha_Event)
MOVE 0 Sasha [15,0]
ENUN
DISA Sasha
NoFade
ENDA

CharacterBasedEvents:
CharacterEventBothWays(0x10,TalkEventMatthias, Edgar, Matthias)
CharacterEventBothWays(0x11,TalkEventStella, Edgar, Stella)
END_MAIN

TalkEventMatthias:
MUSC EO3_ThoseWhoSlayAndFall
Text(TEXT_Ch1_Edgar_Matthias_Talk)
NoFade
CUSA Matthias
ENDA

TalkEventStella:
MUSC EO3_ThoseWhoSlayAndFall
Text(TEXT_Ch1_Edgar_Stella_Talk)
NoFade
ENDA

LocationBasedEvents:
Escape(18,2)
Village(0x8,VillageEvent2,2,1)
Village(0x9,VillageEvent1,16,8)
END_MAIN

VillageEvent1:
	VillageEventItem(TEXT_Ch1_Village_SteelBlade, Village, SteelBlade)

VillageEvent2: // Different dialogue for Stella
	MUSI
	SMOV 0x2 Village
	
	CHECK_ACTIVE
	SVAL 1 Stella
	BNE 3 1 0xC
	SMOV 0x3 TEXT_Ch1_Village_Thunder_Stella
	GOTO 4
	LABEL 3
	SMOV 0x3 TEXT_Ch1_Village_Thunder
	LABEL 4
	
	CALL $9EE310; MUNO; CALL $591F40; SMOV 0x3 Thunder; GIVEITEMTO 0xFFFF; EVBIT_T 0x7; ENDA

MiscBasedEvents:
CauseGameOverIfLordDies
END_MAIN

Dunno1:
END_MAIN

Dunno2:
END_MAIN

Dunno3:
END_MAIN

Tutorial:
END_MAIN

StellaReinforcementUnits:
UNIT Stella Mage_F 0x1 Level(1,Ally,0)   [0,6] 0b 0x0 0x1 StellaReda [Fire,Vuln,0x0] [0x0,0x0,0x0,0x0]
UNIT

Units1:
UNIT Edgar  0x1 0x1 Level(1,Ally,0)   [0,10] 0x0 0x0 0x1 EdgarReda [IronLance,RegalLance,Vuln] NoAI
UNIT Jenna  Archer_F 0x1 Level(1,Ally,0)   [0,10] 0x0 0x0 0x0 0x0 [IronBow,Vuln] NoAI
UNIT Duncan  Fighter 0x1 Level(1,Ally,0)   [0,9] 0x0 0x0 0x1 DuncanReda [IronAxe,Vuln] NoAI
UNIT Kurt  Mercenary 0x1 Level(1,Ally,0)   [0,11] 0x0 0x0 0x1 KurtReda [IronSword,Vuln] NoAI
UNIT
EdgarReda:
REDA [2,10] 0x0 0x1 0x00 0x0
DuncanReda:
REDA [1,9] 0x0 0x1 0x00 0x0
KurtReda:
REDA [2,11] 0x0 0x1 0x00 0x0
StellaReda:
REDA [1,6] 0x0 0x1 0x00 0x0

EnemyUnits1:
// Unique
//UNIT Ch1Boss Knight 0x40 Level(4,Enemy,0)     [18,2] 0x0 0x0 0x0 0x0 [SteelAxe,HandAxe] NeverMoveAI
//UNIT Ch1Boss Knight 0x40 Level(4,Enemy,0)     [8,7] 0x0 0x0 0x0 0x0 [SteelAxe,HandAxe] NeverMoveAI
UNIT Ch1Boss SwKnight 0x40 Level(4,Enemy,0)     [8,7] 0x0 0x0 0x0 0x0 [LightBrand,SteelSword] NeverMoveAI
UNIT Matthias Soldier 0x40 Level(3,Enemy,0)     [7,7] 0x0 0x0 0x0 0x0 [IronLance,Vuln] NeverMoveAI
UNIT Sasha Myrmidon_F 0x40 Level(5,Enemy,0)     [17,2] 0x0 0x0 0x0 0x0 [SlimSword,SteelBlade,FireSword,Vuln] DoNothing

UNIT 0x84 Soldier 0x40 Level(1,Enemy,1)     [1,2] 0x0 0x0 0x0 0x0 [IronLance] DoNothing // Cutscene enemy

// Generics
UNIT 0x80 Knight 0x40 Level(1,Enemy,1)     [12,3] DropItem 0x0 0x0 0x0 [HandAxe] NeverMoveAI
UNIT 0x80 Mage 0x40 Level(1,Enemy,1)     [14,1] 0x0 0x0 0x0 0x0 [Fire] AIRAI
UNIT 0x80 Myrmidon 0x40 Level(1,Enemy,1)     [14,7] 0x0 0x0 0x0 0x0 [IronSword] AIRAI

UNIT 0x80 Soldier 0x40 Level(1,Enemy,1)     [8,4] DropItem 0x0 0x0 0x0 [SteelLance] AIRAI
#ifndef _EASY_MODE_
UNIT 0x80 Mage 0x40 Level(1,Enemy,1)     [11,8] 0x0 0x0 0x0 0x0 [Fire] PWHAI
#endif
UNIT

EnemyUnits2:
UNIT 0x80 Soldier 0x40 Level(1,Enemy,1)     [10,8] 0x0 0x0 0x0 0x0 [IronLance] PWHAI
UNIT 0x80 Mercenary 0x40 Level(1,Enemy,1)     [6,6] 0x0 0x0 0x0 0x0 [IronSword] PWHAI

UNIT 0x80 Mercenary 0x40 Level(1,Enemy,1)     [5,2] DropItem 0x0 0x0 0x0 [IronBlade] AIRAI
UNIT 0x80 Cavalier 0x40 Level(1,Enemy,1)     [8,1] 0x0 0x0 0x0 0x0 [IronLance] AIRAI

//UNIT 0x80 Myrmidon 0x40 Level(1,Enemy,1)     [15,3] 0x0 0x0 0x0 0x0 [IronSword] PWHAI
UNIT 0x80 Myrmidon 0x40 Level(1,Enemy,1)     [15,2] 0x0 0x0 0x0 0x0 [IronSword] PWHAI
#ifndef _EASY_MODE_
//UNIT 0x80 Fighter 0x40 Level(1,Enemy,1)     [16,2] 0x0 0x0 0x0 0x0 [IronAxe] PWHAI
UNIT 0x80 Fighter 0x40 Level(1,Enemy,1)     [16,1] 0x0 0x0 0x0 0x0 [IronAxe] PWHAI
#endif

UNIT 0x80 Knight 0x40 Level(1,Enemy,1)     [14,10] 0x0 0x0 0x0 0x0 [IronAxe] AIRAI
UNIT 0x80 Archer_F 0x40 Level(1,Enemy,1)     [15,10] 0x0 0x0 0x0 0x0 [IronBow] AIRAI

UNIT 0x80 Soldier 0x40 Level(1,Enemy,1)     [16,4] DropItem 0x0 0x0 0x0 [Javelin] AIRAI
UNIT 0x80 Mercenary 0x40 Level(1,Enemy,1)     [15,4] 0x0 0x0 0x0 0x0 [IronSword] AIRAI

UNIT 0x80 Mage_F 0x40 Level(1,Enemy,1)     [18,1] 0x0 0x0 0x0 0x0 [Fire] NeverMoveAI
UNIT 0x80 Soldier 0x40 Level(1,Enemy,1)     [18,3] 0x0 0x0 0x0 0x0 [Javelin] NeverMoveAI

//UNIT 0x82 Brigand 0x40 Level(1,Enemy,1)     [5,10] 0x0 0x0 0x0 0x0 [IronAxe] AttackingBrigandAI
UNIT 0x82 Brigand 0x40 Level(1,Enemy,1)     [5,10] 0x0 0x0 0x0 0x0 [SteelAxe] AttackingBrigandAI
UNIT

EnemyReinforcementUnits1:
UNIT 0x80 Mercenary 0x40 Level(1,Enemy,1)     [9,0] 0x0 0x0 0x0 0x0 [IronSword] PWHAI
UNIT 0x80 Mage_F 0x40 Level(1,Enemy,1)     [11,0] 0x0 0x0 0x0 0x0 [Fire] PWHAI
UNIT

EnemyReinforcementUnits2:
UNIT 0x80 Archer 0x40 Level(1,Enemy,1)     [2,12] 0x0 0x0 0x0 0x0 [IronBow] PWHAI
UNIT 0x80 Myrmidon_F 0x40 Level(1,Enemy,1)     [3,12] 0x0 0x0 0x0 0x0 [IronSword] PWHAI
UNIT

EvilReinforcementUnits:
UNIT 0x81 Soldier 0x40 Level(10,Enemy,1)     [0,11] 0x0 0x0 0x0 0x0 [SilverLance,ShortSpear] PWHAI
UNIT 0x81 Mercenary 0x40 Level(10,Enemy,1)     [0,10] 0x0 0x0 0x0 0x0 [SilverSword,LightBrand] PWHAI
UNIT 0x81 Fighter 0x40 Level(10,Enemy,1)     [0,9] 0x0 0x0 0x0 0x0 [SilverAxe,HandAxe] PWHAI
UNIT

BrigandReinforcementUnits1:
UNIT 0x82 Brigand 0x40 Level(1,Enemy,1)     [15,0] 0x0 0x0 0x0 0x0 [HandAxe] AttackingBrigandAI
UNIT

BrigandReinforcementUnits2:
UNIT 0x82 Brigand 0x40 Level(1,Enemy,1)     [18,4] 0x0 0x0 0x0 0x0 [IronAxe] AttackingBrigandAI
UNIT

Traps1:
ENDTRAP
ALIGN 4

Traps2:
ENDTRAP
ALIGN 4

BeginningScene:
// Disable runes entirely for this chapter
ENUT RuneMenuDisableFlag
// Preemptively disable all runes
ENUT MiracleRuneDisabledFlag
ENUT CelerityRuneDisabledFlag
ENUT SwarpRuneDisabledFlag
ENUT 0x75
ENUT 0x76
ENUT 0x77
ENUT 0x78
ENUT 0x7A
ENUT 0x7B
ENUT 0x7C
ENUT 0x7D
ENUT 0x7E
ENUT 0x7F

/*
ENUT 0x129 // AllUnitsMustEscapeFlag
MUSC Langrisser_Story
CAM1 [0,11]
LoadEnemies(EnemyUnits1)
LoadEnemies(EnemyUnits2)
SetBackground(0x5)
TUTORIALTEXTBOXSTART
CenterTutorialTextBox
TEXTSHOW TEXT_Ch1_Intro
TEXTEND
MUSC BoF2_StrongerMonsters
ClearBackground
//FADU 16
LoadPlayerUnits(Units1)
*/

/*
SetBackground(0x5)
MUSC Langrisser_Story
TUTORIALTEXTBOXSTART
CenterTutorialTextBox
TEXTSHOW TEXT_RuneTutorial
TEXTEND
ClearBackground
NoFade
*/

ENUT 0x129 // AllUnitsMustEscapeFlag
MUSC BoF2_StrongerMonsters
CAM1 [0,11]
LoadEnemies(EnemyUnits1)
LoadEnemies(EnemyUnits2)
FADU 16
LoadPlayerUnits(Units1)


Text(TEXT_Ch1_Start_Edgar)
MUSC DW3_Oppression
//MUSC AA3_VictorKudo
FlashCursor(Matthias,0x20)
FlashCursor(Ch1Boss,0x20)
Text(TEXT_Ch1_Start_Matthias)
MOVE 15 Ch1Boss [18,2]

NoFade
ENDA

EndingScene:
MUSC ToS_LastBattleDecision
SetBackground(0x2d)
Text(TEXT_Ch1_End)
GiveItem(SteelLance,Edgar)
GiveItem(SteelBow,Jenna)
GiveItem(SteelAxe,Duncan)
GiveItem(SteelSword,Kurt)
MNC2 Chapter2
ENDA
